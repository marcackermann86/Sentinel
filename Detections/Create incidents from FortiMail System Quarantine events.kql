let adminOpenedRegex = @"^Administrator \w+@<home domain> opened System quarantine message '\w{14,19}-\w{14,19}' from (\d{1,3}\.){4}$";
let adminReleasedRegex = @"^Web release: System Quarantine message '\w{14,19}-\w{14,19}' of folder \w+ was released via web link from (\d{1,3}\.){4}$";
let sandboxMsgRegex = @"^Email \w{14,19}-\w{14,19} has been processed by FortiSandbox, \d+s used$|^queued for FortiSandbox scan, since it contains .+$|^.* has been sent to FortiSandbox$";
let fullRegex = strcat("^DMARC|^DKIM|^SPF|", adminOpenedRegex, "|", adminReleasedRegex, "|", sandboxMsgRegex);
FortiMailHistory
| where disposition has "System Quarantine" and direction == "in"
| join kind=leftouter (FortiMailSpam
    | where not(msg matches regex fullRegex)
    | summarize spamResults=make_list(msg) by session_id)
    on session_id
| join kind=leftouter (FortiMailVirus
    | where not(msg matches regex fullRegex)
    | summarize virusResults=make_list(msg) by session_id)
    on session_id
| project TimeGenerated, headerFrom, sender, recipient, subject, classifier, detail, spamResults, virusResults, country=client_cc, ip=client_ip, message_id, session_id, productName="FortiMail"
