let frequency = 1h;
let lookback = 14d;
OfficeActivity
| where TimeGenerated >= ago(frequency)
| where Operation in ("Set-Mailbox", "New-InboxRule", "Set-InboxRule") 
| where Parameters has_any ("ForwardingAddress", "ForwardingSmtpAddress", "ForwardTo", "ForwardAsAttachmentTo", "RedirectTo")
| mv-apply Params=todynamic(Parameters) on 
    ( project 
        InboxRule = iff(Params.Name == 'Name', Params.Value, ""),
        ForwardedTo = iff(Params.Name in ("ForwardingAddress", "ForwardingSmtpAddress", "ForwardTo", "ForwardAsAttachmentTo", "RedirectTo"), Params.Value, ""),
        ForwardFrom = iff(Params.Name == 'From', Params.Value, ""),
        Mailbox = iff(Operation != "Set-InboxRule" and Params.Name == 'Identity', iff(Params.Value has '/', tostring(split(Params.Value, '/')[-1]), Params.Value), ""),
        SourceRecordId
    | summarize take_any(InboxRule), take_any(ForwardFrom), take_any(ForwardedTo), take_any(Mailbox)  by SourceRecordId)
| where isnotempty(ForwardedTo)
| where ForwardedTo !endswith "@<home domain>"
| join kind=leftouter (IdentityInfo 
    | where TimeGenerated >= ago(lookback) 
    | summarize arg_max(TimeGenerated, *) by AccountObjectId 
    | project AccountObjectId, AccountUPN) 
    on $left.Mailbox == $right.AccountObjectId
| extend Mailbox = coalesce(AccountUPN, Mailbox, UserId, "")
| project TimeGenerated, Operation, RuleCreatedBy=UserId, CreatedByIp=tostring(split(ClientIP, ':')[0]), InboxRule, Mailbox, ForwardFrom=coalesce(ForwardFrom, "All emails"), ForwardedTo
